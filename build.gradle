plugins {
    id 'java'
    id 'idea'
    id 'org.jetbrains.kotlin.jvm' version '1.2.41'
    id 'org.jetbrains.kotlin.kapt' version '1.2.41'
    id 'de.fuerstenau.buildconfig' version '1.1.8'
}

group 'com.github.rothso.mass'
version '1.0'

sourceCompatibility = 1.8

buildConfig {
    // Secrets are defined in a file called "gradle.properties"
    buildConfigField 'String', 'ATHENA_KEY', athenaKey
    buildConfigField 'String', 'ATHENA_SECRET', athenaSecret
}

repositories {
    maven { url 'http://dl.bintray.com/kotlin/kotlin-eap' }
    mavenCentral()
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'org.jetbrains.kotlin:kotlin-reflect'

    // HTTP Client
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.4.0'
    implementation 'com.squareup.retrofit2:converter-moshi:2.4.0'

    // Extra dependencies for Retrofit
    implementation 'io.reactivex.rxjava2:rxjava:2.2.0'
    implementation 'com.squareup.moshi:moshi-kotlin:1.6.0'
    kapt 'com.squareup.moshi:moshi-kotlin-codegen:1.6.0'

    // Faker
    implementation 'com.github.javafaker:javafaker:0.15'
    
    // Terminal styling
    implementation 'com.github.ajalt:mordant:1.2.0'

    testImplementation 'junit:junit:4.12'
}

jar {
    manifest {
        attributes 'Main-Class': 'com.github.rothso.mass.extractor.MainKt'
    }

    // This line of code recursively collects and copies all of a project's files
    // and adds them to the JAR itself. One can extend this task, to skip certain
    // files or particular types at will
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
